{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "9bc1b0a8",
   "metadata": {},
   "source": [
    "## Analysing the experimentla data of  [Brydges et al, Science 2019](https://doi.org/10.1126/science.aau4963)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "id": "2a2c0683-6ec3-4b0e-ba9c-5f94cac50deb",
   "metadata": {},
   "outputs": [],
   "source": [
    "using ITensors\n",
    "using RandomMeas\n",
    "using ProgressMeter\n",
    "using MAT"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "id": "4fcfc571-3cd1-4e1d-8ae5-39cfedd8a94b",
   "metadata": {},
   "outputs": [],
   "source": [
    "## parameters \n",
    "N = 10 # System size\n",
    "times = [0,1,2,3,4,5] # quench times in ms\n",
    "ntimes = length(times)  # number of time points\n",
    "ξ = siteinds(\"Qubit\", N); # site indices of the shadows\n",
    "\n",
    "# Parameters specifying the Renyi entropies of interest\n",
    "n = 6 # maximal Renyi index (n-th Renyi entropy)\n",
    "NAmax = 6; #maximal subsystem size. We always consider subsystems of the form [1,2,...,NA]."
   ]
  },
  {
   "cell_type": "markdown",
   "id": "f0d8f9a5",
   "metadata": {},
   "source": [
    "We now load the experimental randomized measurement data obtained in Brydges et al., Science 2018."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 4,
   "id": "f3ef5c7c",
   "metadata": {},
   "outputs": [],
   "source": [
    "NU, NM = 500,150 # number of unitaries and measurements per unitary\n",
    "\n",
    "data = zeros(Int,ntimes,NU,NM,N); # data array containgin the measurement results\n",
    "\n",
    "u = Vector{Vector{Vector{ITensor}}}()\n",
    "for s in 1:ntimes\n",
    "    vars = matread(\"BrydgesScience2019data/unitaries_10_T_\" * string(times[s]) * \"_flr_1.mat\")\n",
    "    raw_measurements = vars[\"qstates\" * string(times[s])]\n",
    "\n",
    "    for r in 1:NU\n",
    "        for m in 1:NM\n",
    "            data[s,r,m,:] = reverse(digits(floor(Int,raw_measurements[r,m]), base=2, pad=N) .+ 1)\n",
    "        end\n",
    "    end    \n",
    "    \n",
    "    ut = Vector{Vector{ITensor}}()\n",
    "    for r in 1:NU\n",
    "        push!(ut,[ITensor(vars[\"unitaries\"][r,i],ξ[i]',ξ[i]) for i in 1:N])\n",
    "    end\n",
    "    push!(u,ut)\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "16dd0a66",
   "metadata": {},
   "source": [
    "We estimate the purity using the direct purity estimation formula presented Brydges et al., Science 2019 and using classical shadows Huang et al., Nat. Phys. 2020."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 5,
   "id": "d4bcb43e",
   "metadata": {},
   "outputs": [],
   "source": [
    "p_direct = zeros(Float64,ntimes,NAmax)\n",
    "p_shadow = zeros(Float64,ntimes,NAmax)\n",
    "@showprogress for s in 1:ntimes\n",
    "    for NA in 1:NAmax\n",
    "        uA = [ut[1:NA] for ut in u[s]]\n",
    "        p_direct[s,NA] = get_purity_hamming(data[s,:,:,1:NA],ξ[1:NA]) \n",
    "        p_shadow[s,NA] = get_purity_shadows(data[s,:,:,1:NA],uA,ξ[1:NA])\n",
    "    end\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "a791b037",
   "metadata": {},
   "source": [
    "We estimate higher order Renyi entropies (up to order n) using batch shadows (c.f. Rath et al., PRXQ 2023)."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "bac4974b",
   "metadata": {},
   "outputs": [],
   "source": [
    "#Tr(rho^n) and Renyis from batch shadows\n",
    "p_bshadow = zeros(Float64,ntimes,NAmax,n-1)\n",
    "S_bshadow = zeros(Float64,ntimes,NAmax,n-1)\n",
    "@showprogress for s in 1:ntimes\n",
    "    for NA in 1:NAmax\n",
    "        uA = [ut[1:NA] for ut in u[s]]\n",
    "        ξA = ξ[1:NA]\n",
    "        ρs = get_batch_shadows(data[s,:,:,1:NA],ξA,uA,n)\n",
    "        p_bshadow[s,NA,:] = get_moments(ρs, ξA, collect(2:n))\n",
    "        for nt in 2:n\n",
    "            S_bshadow[s,NA,nt-1]= log2(p_bshadow[s,NA,nt-1])/(1-nt)\n",
    "        end\n",
    "    end\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "cd346413",
   "metadata": {},
   "source": [
    "Finally, we load quantum states generated by classical simulations (master equation evolution) of the experiment, including a decoherence model. We compute Renyi entropies."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "be33fbde-b611-48d1-9920-f3230a0c792a",
   "metadata": {},
   "outputs": [],
   "source": [
    "ρ = Vector{ITensor}()\n",
    "p = zeros(Float64,ntimes,NAmax,n-1)\n",
    "S = zeros(Float64,ntimes,NAmax,n-1)\n",
    "SvN = zeros(Float64,ntimes,NAmax)\n",
    "ITensors.disable_warn_order()\n",
    "\n",
    "for s in 1:ntimes\n",
    "    qstate = matread(\"BrydgesScience2019data/rho_10_XY_10_-1.00_0.00\"*string(times[s])*\"_1_1_1_flr_1.mat\")[\"rho\"]\n",
    "    qstate = reshape(qstate,tuple((2*ones(Int,2*N))...))\n",
    "    push!(ρ,ITensor(qstate,vcat(ξ',ξ)))\n",
    "    for NA in 1:NAmax\n",
    "        A = collect(1:NA)\n",
    "        ρA = copy(ρ[s])\n",
    "        for i in 1:N\n",
    "            if !(i in A)\n",
    "                ρA *= δ(ξ[i],ξ[i]') \n",
    "            end\n",
    "        end\n",
    "        for nt in 2:n\n",
    "            p[s,NA,nt-1] = real(trace(power(ρA,nt),ξ[1:NA]))\n",
    "            S[s,NA,nt-1]= log2(p[s,NA,nt-1])/(1-nt)\n",
    "        end\n",
    "        D, = eigen(ρA,ξ[1:NA]',ξ[1:NA];ishermitian=true)\n",
    "        for k in 1:2^NA\n",
    "            if D[k,k]>1e-12\n",
    "                SvN[s,NA] -= D[k,k]*log2(D[k,k])\n",
    "            end\n",
    "        end\n",
    "    end\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "406a295a",
   "metadata": {},
   "source": [
    "We plot the purity as a function of NA for the different times."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "a1ab698f",
   "metadata": {},
   "outputs": [],
   "source": [
    "using PyPlot\n",
    "\n",
    "clf()\n",
    "\n",
    "cm = get_cmap(:viridis)\n",
    "for s in 1:ntimes\n",
    "    plot(1:NAmax,p[s,:,1],\"--\",color=cm(s/ntimes))\n",
    "    plot(1:NAmax,p_direct[s,:],\"o\",color=cm(s/ntimes))\n",
    "    plot(1:NAmax,p_shadow[s,:],\"x\",color=cm(s/ntimes))\n",
    "    plot(1:NAmax,p_bshadow[s,:,1],\"*\",color=cm(s/ntimes))\n",
    "end\n",
    "#yscale(\"log\")\n",
    "xlabel(\"Partition\")\n",
    "ylabel(\"Purity\")\n",
    "legend([\"theory\",\"hamming\",\"shadow\",\"batch_shadow\"])\n",
    "title(\"Purity for different times\")\n",
    "\n",
    "gcf()"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "a8845aa2",
   "metadata": {},
   "source": [
    "We plot the higher order Renyi entropies as a function of Renyi index n for a fixed subystem size and various times."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "a3a1fdc2",
   "metadata": {},
   "outputs": [],
   "source": [
    "using PyPlot\n",
    "\n",
    "clf()\n",
    "\n",
    "NA = 6\n",
    "for s in 1:1\n",
    "    plot(2:n,p[s,NA,:],\"--\",color=cm(s/ntimes),label=\"theory\")\n",
    "    plot(2:n,p_bshadow[s,NA,:],\"x\",color=cm(s/ntimes),label=\"batch_shadow\")\n",
    "end\n",
    "for s in 2:ntimes\n",
    "    plot(2:n,p[s,NA,:],\"--\",color=cm(s/ntimes))\n",
    "    plot(2:n,p_bshadow[s,NA,:],\"x\",color=cm(s/ntimes))\n",
    "end\n",
    "xlabel(\"n\")\n",
    "ylabel(L\"p_n(\\rho_A)\")\n",
    "legend(title = \"NA= $NA \")\n",
    "\n",
    "gcf()"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.10.4",
   "language": "julia",
   "name": "julia-1.10"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.10.4"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
